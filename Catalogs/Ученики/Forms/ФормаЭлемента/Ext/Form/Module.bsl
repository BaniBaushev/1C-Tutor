
#Область ОписаниеПеременных

#КонецОбласти

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ИсторияУроков.Параметры.УстановитьЗначениеПараметра("Ученик", Объект.Ссылка);  
	ИсторияОплат.Параметры.УстановитьЗначениеПараметра("Ученик", Объект.Ссылка);
	
	Баланс = БалансУченика(Объект.Ссылка);
	
	ТекущееВремяУченика = Справочники.ЧасовыеПояса.РасчетТекущегоВремениУченика(Объект.ЧасовойПояс);
	
КонецПроцедуры

&НаСервере
Процедура ПриЗаписиНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	Если Отказ Тогда Возврат КонецЕсли;
	
	НачатьТранзакцию();
	
	НаборЗаписей = РегистрыСведений.ТарифыУчеников.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.Ученик.Установить(ТекущийОбъект.Ссылка);
	
	НоваяЗапись = НаборЗаписей.Добавить();
	НоваяЗапись.Тариф = ТекущийОбъект.Тариф;
	НоваяЗапись.Ученик = ТекущийОбъект.Ссылка;
    НоваяЗапись.Период = ТекущаяДатаСеанса();
	
	Попытка
		НаборЗаписей.Записать();
        ЗафиксироватьТранзакцию();
	Исключение
		ОтменитьТранзакцию();
		ПодробноеПредставлениеОшибки = ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		ТекстОшибки = СтрШаблон("При записи тарифа ученика произошла ошибка. Подробное описание: %1", ПодробноеПредставлениеОшибки);
		ОбщегоНазначения.СообщитьПользователю(ТекстОшибки);
		Отказ = Истина;
		Возврат;
	КонецПопытки;	
		
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

// Код процедур и функций

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура СоздатьНовыйУрок(Команда)

	ЭлементыОтбора = Новый Структура("Ученик", Объект.Ссылка);
	ПараметрыФормы = Новый Структура("ЗначенияЗаполнения", ЭлементыОтбора);
	ОткрытьФорму("Документ.Уроки.Форма.ФормаДокумента", ПараметрыФормы);
	
КонецПроцедуры

&НаКлиенте
Процедура ПополнитьБалансУченика(Команда) 
	
	ЭлементыОтбора = Новый Структура("Ученик", Объект.Ссылка);
	ПараметрыФормы = Новый Структура("ЗначенияЗаполнения", ЭлементыОтбора);
	ОткрытьФорму("Документ.ПоступлениеДС.Форма.ФормаДокумента", ПараметрыФормы);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Функция БалансУченика(Ученик)
	
	Запрос = Новый Запрос();
	Запрос.Текст = "ВЫБРАТЬ
	               |	БалансУченикаОстатки.Ученик КАК Ученик,
	               |	БалансУченикаОстатки.СуммаОстаток КАК СуммаОстаток
	               |ИЗ
	               |	РегистрНакопления.БалансУченика.Остатки КАК БалансУченикаОстатки
	               |ГДЕ
	               |	БалансУченикаОстатки.Ученик = &Ученик";  
	
	Запрос.УстановитьПараметр("Ученик", Ученик);
	РезультатЗапроса = Запрос.Выполнить();
	Выборка = РезультатЗапроса.Выбрать();
	
	Баланс = 0;
	
	Если Выборка.Следующий() Тогда 
		
		Баланс = Выборка.СуммаОстаток;
		
	КонецЕсли;
	
	Возврат Баланс;
	
КонецФункции

#КонецОбласти
