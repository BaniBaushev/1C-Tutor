
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Элементы.ТекущаяДата.Заголовок = Формат(ТекущаяДатаСеанса(), "Л=ru_RU; ДЛФ=DD");
	ПериодОтчета = Формат(ТекущаяДата(), "ДФ=MMMM.гггг");
	ДатаОтчета = ТекущаяДата();
	РассчитатьПоказателиЗаПериод();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы 

&НаКлиенте
Процедура СледующийМесяц(Команда)
	 ИзменитьПериод(1); // -1 месяц
КонецПроцедуры

&НаКлиенте
Процедура ПредыдущийМесяц(Команда)
	ИзменитьПериод(-1); // -1 месяц
КонецПроцедуры

&НаКлиенте
Процедура ПериодОтчетаНажатие(Элемент, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	ВыбратьПериод();
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции 

&НаКлиенте
Процедура ВыбратьПериод()
    // Создаем объект для выбора периода
    Диалог = Новый ДиалогРедактированияСтандартногоПериода();
    Диалог.Период = Новый СтандартныйПериод(ДатаОтчета, ДатаОтчета);
    
    // Устанавливаем вариант периода (месяц)
    Диалог.Показать(Новый ОписаниеОповещения("ВыбратьПериодЗавершение", ЭтотОбъект));
КонецПроцедуры

&НаКлиенте
Процедура ВыбратьПериодЗавершение(РезультатВыбора, ДополнительныеПараметры) Экспорт
    Если РезультатВыбора <> Неопределено Тогда
        ДатаОтчета = РезультатВыбора.ДатаНачала; 
		ПериодОтчета = Формат(ДатаОтчета, "ДФ=MMMM.гггг");
    	РассчитатьПоказателиЗаПериод();
    КонецЕсли;
КонецПроцедуры

// Общая функция изменения периода
&НаКлиенте
Процедура ИзменитьПериод(Смещение)
    
    // Получаем первый день текущего месяца
    НоваяДата = НачалоМесяца(ДатаОтчета);
    
    // Применяем смещение
    НоваяДата = ДобавитьМесяц(НоваяДата, Смещение);
    
    // Обновляем значение периода
    ДатаОтчета = НоваяДата;
	
	ПериодОтчета = Формат(ДатаОтчета, "ДФ=MMMM.гггг");
	
    // Обновляем данные на форме
    РассчитатьПоказателиЗаПериод();
    
КонецПроцедуры

&НаСервере
Процедура РассчитатьПоказателиЗаПериод()
	
    ПроведеноУроков = Документы.Уроки.КоличествоУроковЗаПериод(НачалоМесяца(ДатаОтчета), КонецМесяца(ДатаОтчета));
	Заработано = РегистрыНакопления.БалансУченика.ПрибыльЗаПериод(НачалоМесяца(ДатаОтчета), КонецМесяца(ДатаОтчета));
	СуммаНалога = Заработано * 0.04;
	ЧистаяПрибыль = Заработано - СуммаНалога - ДополнительныеРасходы;
	
КонецПроцедуры



#КонецОбласти
